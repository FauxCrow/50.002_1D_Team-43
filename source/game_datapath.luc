module game_datapath #(
    SLOW_CLOCK_DIV ~ 26 : SLOW_CLOCK_DIV > 0)
(
    input p1_button,
    input p2_button,
    input reset_button,
    
    input clk,  // clock
    input rst,  // reset
    
    output debug_out[32],
    output p1_row[8],
    output p1_prev_row[8],
    output p1_direction[1],
    output p2_row[8],
    output p2_prev_row[8],
    output p2_direction[1],
    output level[5],
    output p1_win[1],
    output p2_win[1],
    output turn[1],
    output both_win[1],
    //output p2_out[16],
    output debug_slowclock
) {
    
    //temp variables
    sig inputAlu_a[32]
    sig inputAlu_b[32]
    
    alu game_alu
    
    .clk(clk){
        edge_detector game_timer_detector(#RISE(1), #FALL(0))
        
        .rst(rst){
            game_cu main_control_unit
            game_regfiles players
            
            counter slow_timer(#SIZE(1),#DIV(SLOW_CLOCK_DIV)) 
            
        }
    }
    always {
        
        //edge detectors
        game_timer_detector.in = slow_timer.value // shift timer detector
        
        //pass timer signals to CU unit 
        main_control_unit.shift_timer = game_timer_detector.out
        
        //pass button press signal to CU unit
        main_control_unit.p1_button = p1_button
        main_control_unit.p2_button = p2_button
        main_control_unit.reset_button = reset_button
        
        //Setup REGFILE unit 
        players.we = main_control_unit.we_regfile
        players.write_address = main_control_unit.regfile_write_address
        
        players.read_address_a = main_control_unit.regfile_read_address_a
        players.read_address_b = main_control_unit.regfile_read_address_b
        
        //bootstrap reg_b output to CU for convenient branching conditions
        main_control_unit.regfile_datain = players.out_b
        
        //asel mux
        case(main_control_unit.asel){
            b00  : inputAlu_a = players.out_a
            b01  : inputAlu_a = 0
            
            default : //necessary to init
                inputAlu_a = 0
        }
        
        //bsel mux
        case(main_control_unit.bsel){
            b00 : inputAlu_b = players.out_b
            b01 : inputAlu_b = 1 // used to add / shift 1
            b11 : inputAlu_b = 60 // used to set from 00000000 to 00111100
            
            default : //necessary to init
                inputAlu_b = 0
        }
        
        //ALU
        game_alu.a = inputAlu_a
        game_alu.b = inputAlu_b
        game_alu.alufn = main_control_unit.alufn // take alufn from fsm
        
        //wdsel mux
        case (main_control_unit.alu_out_sel){
            b01 : players.data = b1111111111111111 // winning
            b10 : players.data = b0000000000001111 // losing
            b11 : players.data = 0 //draw
            default : //b00
                players.data = game_alu.out
        }
        
        //for debugging       
        debug_out = players.timer
        p1_row = players.p1_row
        p1_prev_row = players.p1_prev_row
        p1_direction = players.p1_direction
        level = players.level
        p1_win = players.p1_win
        p2_win= players.p2_win
        both_win = players.both_win
        
        p2_row = players.p2_row
        p2_prev_row = players.p2_prev_row
        p2_direction = players.p2_direction
        turn = players.turn
        
        debug_slowclock = slow_timer.value
    }
}